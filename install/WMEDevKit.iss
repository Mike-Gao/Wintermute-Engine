; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppID=WmeDevKit
AppName=Wintermute Engine Development Kit (July 19, 2012)
AppVerName=Wintermute Engine Development Kit 1.10.1
AppPublisher=Dead:Code Software
AppPublisherURL=http://www.dead-code.org
AppSupportURL=http://www.dead-code.org
AppUpdatesURL=http://www.dead-code.org
DefaultDirName={pf}\WME DevKit
DefaultGroupName=Wintermute Engine Development Kit
AllowNoIcons=yes
ChangesAssociations=yes
LicenseFile="source\license.txt"
SetupIconFile=wme.ico
OutputBaseFilename=wme_devkit_1101
;Compression=bzip/9
Compression=lzma/max
SolidCompression=yes
MergeDuplicateFiles=yes
WizardImageFile=compiler:WizModernImage-IS.bmp
WizardSmallImageFile=compiler:WizModernSmallImage-IS.bmp

VersionInfoCompany=Dead:Code Software
VersionInfoCopyright=Copyright (c) Dead:Code Software 2012
VersionInfoDescription=Wintermute Engine Development Kit
VersionInfoTextVersion=1.10.1
VersionInfoVersion=1.10.1


[Tasks]
Name: "desktopicon"; Description: "Create a &desktop icon"; GroupDescription: "Additional icons:"
Name: "quicklaunchicon"; Description: "Create a &Quick Launch icon"; GroupDescription: "Additional icons:"; Flags: unchecked
Name: "scriptassociate"; Description: "Associate script files with the provided script editor"; GroupDescription: "Additional icons:"

[Run]
Filename: "{tmp}\vcredist_x86.exe"; Parameters: "/qb";

[Files]
Source: "redist\vcredist_x86.exe"; DestDir: "{tmp}"

Source: "source\*.*"; DestDir: "{app}"; Flags: ignoreversion

; SciTe
Source: "source\SciTe\*.*"; DestDir: "{app}\SciTE"; Flags: ignoreversion recursesubdirs createallsubdirs

; templates
Source: "source\template\*.*"; DestDir: "{app}\template"; Flags: ignoreversion recursesubdirs createallsubdirs

; projects
Source: "source\projects\*.*"; DestDir: "{app}\projects"; Flags: ignoreversion recursesubdirs createallsubdirs

; help
Source: "source\help\*.*"; DestDir: "{app}\help"; Flags: ignoreversion recursesubdirs

; loc
Source: "source\loc\*.*"; DestDir: "{app}\loc"; Flags: ignoreversion recursesubdirs

; plugins
Source: "source\plugin\*.*"; DestDir: "{app}\plugin"; Flags: ignoreversion recursesubdirs createallsubdirs



; NOTE: Don't use "Flags: ignoreversion" on any shared system files


[INI]
Filename: "{app}\deadcode.url"; Section: "InternetShortcut"; Key: "URL"; String: "http://www.dead-code.org"

[Icons]
Name: "{group}\WME Project Manager"; Filename: "{app}\ProjectMan.exe"
Name: "{group}\SciTE Script Editor"; Filename: "{app}\SciTE\SciTE.exe"
Name: "{group}\SceneEdit"; Filename: "{app}\SceneEdit.exe"
Name: "{group}\SpriteEdit"; Filename: "{app}\SpriteEdit.exe"
Name: "{group}\WindowEdit"; Filename: "{app}\WindowEdit.exe"
Name: "{group}\String Table Manager"; Filename: "{app}\StringTableMgr.exe"; WorkingDir: "{app}"
Name: "{group}\WME Integrator"; Filename: "{app}\Integrator.exe"; WorkingDir: "{app}"
Name: "{group}\Documentation"; Filename: "{app}\wme.chm"
Name: "{group}\Demo project"; Filename: "{app}\projects\wme_demo\wme_demo.wpr"
Name: "{group}\Scene tutorial"; Filename: "{app}\projects\scene_tut\scene_tut.wpr"
Name: "{group}\3D Actors Demo Project"; Filename: "{app}\projects\wme_demo_3d\wme_demo_3d.wpr"
Name: "{group}\Particles Demo Project"; Filename: "{app}\projects\wme_particles\wme_particles.wpr"
Name: "{group}\Plugins Demo Project"; Filename: "{app}\projects\wme_plugins_demo\wme_plugins_demo.wpr"
Name: "{group}\WME on the Web"; Filename: "{app}\deadcode.url"
Name: "{group}\Run demo game"; Filename: "{app}\wme.exe"; Parameters: "-detect -project ""{app}\projects\wme_demo\wme_demo.wpr"""
Name: "{group}\Run 3D demo game"; Filename: "{app}\wme.exe"; Parameters: "-detect -project ""{app}\projects\wme_demo_3d\wme_demo_3d.wpr"""
Name: "{group}\Uninstall Wintermute Engine Development Kit"; Filename: "{uninstallexe}"
Name: "{userdesktop}\WME Project Manager"; Filename: "{app}\ProjectMan.exe"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\WME Project Manager"; Filename: "{app}\ProjectMan.exe"; Tasks: quicklaunchicon

[UninstallDelete]
Type: files; Name: "{app}\deadcode.url"

[Messages]
BeveledLabel=Inno Setup


[Registry]
; project
Root: HKCR; Subkey: ".wpr"; ValueType: string; ValueName: ""; ValueData: "ProjectMan.Document"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "ProjectMan.Document"; ValueType: string; ValueName: ""; ValueData: "WME Project"; Flags: uninsdeletekey
Root: HKCR; Subkey: "ProjectMan.Document\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,1"
Root: HKCR; Subkey: "ProjectMan.Document\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\ProjectMan.exe"" ""%1"""
Root: HKCR; Subkey: "ProjectMan.Document\shell\Run_game"; ValueType: string; ValueName: ""; ValueData: "Run game"
Root: HKCR; Subkey: "ProjectMan.Document\shell\Run_game\command"; ValueType: string; ValueName: ""; ValueData: """{app}\wme.exe"" -project ""%1"" -detect"

; scene
Root: HKCR; Subkey: ".scene"; ValueType: string; ValueName: ""; ValueData: "SceneEdit.Document"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "SceneEdit.Document"; ValueType: string; ValueName: ""; ValueData: "Wintermute Scene"; Flags: uninsdeletekey
Root: HKCR; Subkey: "SceneEdit.Document\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\SceneEdit.exe,1"
Root: HKCR; Subkey: "SceneEdit.Document\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SceneEdit.exe"" ""%1"""
Root: HKCR; Subkey: "SceneEdit.Document\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SceneEdit.exe"" ""%1"""
;Root: HKCR; Subkey: "SceneEdit.Document\shell\open\ddeexec"; ValueType: string; ValueName: ""; ValueData: "[open(""%1"")]"


; sprite
Root: HKCR; Subkey: ".sprite"; ValueType: string; ValueName: ""; ValueData: "SpriteEdit.Document"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "SpriteEdit.Document"; ValueType: string; ValueName: ""; ValueData: "Wintermute Sprite"; Flags: uninsdeletekey
Root: HKCR; Subkey: "SpriteEdit.Document\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\SpriteEdit.exe,1"
Root: HKCR; Subkey: "SpriteEdit.Document\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SpriteEdit.exe"" ""%1"""
Root: HKCR; Subkey: "SpriteEdit.Document\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SpriteEdit.exe"" ""%1"""
;Root: HKCR; Subkey: "SpriteEdit.Document\shell\open\ddeexec"; ValueType: string; ValueName: ""; ValueData: "[open(""%1"")]"

; actor
Root: HKCR; Subkey: ".actor"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Actor"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Actor"; ValueType: string; ValueName: ""; ValueData: "Wintermute Actor"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Actor\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,3"
Root: HKCR; Subkey: "Wintermute.Actor\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""
Root: HKCR; Subkey: "Wintermute.Actor\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""

; actor3d
Root: HKCR; Subkey: ".act3d"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Actor3D"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Actor3D"; ValueType: string; ValueName: ""; ValueData: "Wintermute 3D Actor"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Actor3D\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,9"
Root: HKCR; Subkey: "Wintermute.Actor3D\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""
Root: HKCR; Subkey: "Wintermute.Actor3D\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""

; window
Root: HKCR; Subkey: ".window"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Window"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Window"; ValueType: string; ValueName: ""; ValueData: "Wintermute Window"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Window\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,4"
Root: HKCR; Subkey: "Wintermute.Window\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\WindowEdit.exe"" ""%1"""
Root: HKCR; Subkey: "Wintermute.Window\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\WindowEdit.exe"" ""%1"""

; def
Root: HKCR; Subkey: ".def"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Def"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Def"; ValueType: string; ValueName: ""; ValueData: "Wintermute Generic Definition File"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Def\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,5"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Def\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\WindowEdit"" ""%1"""; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Def\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\WindowEdit"" ""%1"""; Flags: uninsdeletekey

; entity
Root: HKCR; Subkey: ".entity"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Entity"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Entity"; ValueType: string; ValueName: ""; ValueData: "Wintermute Entity"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Entity\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,6"
Root: HKCR; Subkey: "Wintermute.Entity\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""
Root: HKCR; Subkey: "Wintermute.Entity\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""

; font
Root: HKCR; Subkey: ".font"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Font"; Flags: uninsdeletevalue
Root: HKCR; Subkey: ".fnt"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Font"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Font"; ValueType: string; ValueName: ""; ValueData: "Wintermute Font"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Font\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,7"
Root: HKCR; Subkey: "Wintermute.Font\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""
Root: HKCR; Subkey: "Wintermute.Font\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""

; script
Root: HKCR; Subkey: ".script"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Script"; Flags: uninsdeletevalue
Root: HKCR; Subkey: ".inc"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Script"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Script"; ValueType: string; ValueName: ""; ValueData: "Wintermute Script"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Script\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,8"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Script\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""; Flags: uninsdeletekey; Tasks: scriptassociate
Root: HKCR; Subkey: "Wintermute.Script\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""; Flags: uninsdeletekey; Tasks: scriptassociate

; settings
Root: HKCR; Subkey: ".settings"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Settings"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Settings"; ValueType: string; ValueName: ""; ValueData: "Wintermute Startup Settings"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Settings\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,5"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Settings\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Settings\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""; Flags: uninsdeletekey

; game
Root: HKCR; Subkey: ".game"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Game"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Game"; ValueType: string; ValueName: ""; ValueData: "Wintermute Game Settings"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Game\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,5"; Flags: uninsdeletekey

; button
Root: HKCR; Subkey: ".button"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Button"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Button"; ValueType: string; ValueName: ""; ValueData: "Wintermute Button"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Button\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,5"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Button\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Button\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""; Flags: uninsdeletekey


; image
Root: HKCR; Subkey: ".image"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Image"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Image"; ValueType: string; ValueName: ""; ValueData: "Wintermute Tiled Image"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Image\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,5"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Image\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Image\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""; Flags: uninsdeletekey

; items
Root: HKCR; Subkey: ".items"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Items"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Items"; ValueType: string; ValueName: ""; ValueData: "Wintermute Items Definition"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Items\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,5"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Items\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Items\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""; Flags: uninsdeletekey

; tab
Root: HKCR; Subkey: ".tab"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Tab"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Tab"; ValueType: string; ValueName: ""; ValueData: "Wintermute String Table"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Tab\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,5"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Tab\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{win}\notepad.exe"" ""%1"""; Flags: uninsdeletekey

; talk
Root: HKCR; Subkey: ".talk"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Talk"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Talk"; ValueType: string; ValueName: ""; ValueData: "Wintermute Talk File"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Talk\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\ProjectMan.exe,5"; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Talk\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""; Flags: uninsdeletekey
Root: HKCR; Subkey: "Wintermute.Talk\shell\edit\command"; ValueType: string; ValueName: ""; ValueData: """{app}\SciTe\SciTe.exe"" ""%1"""; Flags: uninsdeletekey


; package
Root: HKCR; Subkey: ".dcp"; ValueType: string; ValueName: ""; ValueData: "Wintermute.Package"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "Wintermute.Package"; ValueType: string; ValueName: ""; ValueData: "Wintermute Compiled Package"; Flags: uninsdeletekey


; general settings
Root: HKCU; Subkey: "Software\DEAD:CODE\Wintermute Tools\Settings"; ValueType: string; ValueName: "RepositoryPath1"; ValueData: "{app}\template\";
Root: HKCU; Subkey: "Software\DEAD:CODE\Wintermute Tools\Settings"; ValueType: string; ValueName: "ToolsPath"; ValueData: "{app}\";
Root: HKCU; Subkey: "Software\DEAD:CODE\Wintermute Tools\Settings"; ValueType: dword;  ValueName: "UseAcceleration"; ValueData: "1"; Flags: createvalueifdoesntexist
Root: HKCU; Subkey: "Software\DEAD:CODE\Wintermute Tools\Settings"; ValueType: string; ValueName: "InstallPath"; ValueData: "{app}\";
Root: HKCU; Subkey: "Software\DEAD:CODE\Wintermute Tools\Settings"; ValueType: string; ValueName: "InstallVersion"; ValueData: "1.10.1";

; ProjectMan settings
Root: HKCU; Subkey: "Software\DEAD:CODE\ProjectMan\Settings"; ValueType: string;  ValueName: "LastRepository"; ValueData: "{app}\template\"; Flags: createvalueifdoesntexist
Root: HKCU; Subkey: "Software\DEAD:CODE\ProjectMan\Settings"; ValueType: string;  ValueName: "LastProjectDir"; ValueData: "{userdocs}\WME projects\"; Flags: createvalueifdoesntexist


[Code]
var
  FinishedInstall: Boolean;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssPostInstall then
    FinishedInstall := True;
end;


procedure DeinitializeSetup();
var
  AppDir: String;
  SciteTools: String;
begin
  if FinishedInstall then begin
    AppDir := ExpandConstant('{app}');
    
    FileCopy(AppDir + '\projects\wme_demo_3d\data\music\NaRCo - Postmortusica.ogg', AppDir + '\projects\wme_demo\data\music\NaRCo - Postmortusica.ogg', false);

    { Rename removed project files to .bak }
    RenameFile(AppDir + '\projects\scene_tut\data\scripts\game_daemon.script', AppDir + '\projects\scene_tut\data\scripts\game_daemon.bak');
    RenameFile(AppDir + '\projects\wme_demo\data\scripts\game_daemon.script', AppDir + '\projects\wme_demo\data\scripts\game_daemon.bak');
    RenameFile(AppDir + '\projects\wme_demo_3d\data\scripts\game_daemon.script', AppDir + '\projects\wme_demo_3d\data\scripts\game_daemon.bak');

    { Delete removed template files }
    DeleteFile(AppDir + '\template\project\Basic project\data\scripts\game_daemon.script');


    SciteTools := '';
    SciteTools := SciteTools + 'command.compile.$(file.patterns.script)="' + AppDir + '\wme_comp.exe"  -script "$(FilePath)" -format scite';
    SciteTools := SciteTools + #13#10;
    SciteTools := SciteTools + 'command.help.$(file.patterns.script)=reference!' + AppDir + '\wme.chm';
    SciteTools := SciteTools + #13#10;
    SciteTools := SciteTools + 'command.help.subsystem.$(file.patterns.script)=4';
    SciteTools := SciteTools + #13#10;
    SciteTools := SciteTools + 'api.$(file.patterns.script)=' + AppDir + '\SciTE\wme.api';
    
    SaveStringToFile(AppDir + '\SciTE\wme_tools.properties', SciteTools, False);
  end;
end;


procedure DotNetURLClick(Sender: TObject);
var
  ErrorCode: Integer;
begin
  ShellExec('open', 'http://www.microsoft.com/downloads/details.aspx?FamilyID=0856eacb-4362-4b0d-8edd-aab15c5e04f5', '', '', SW_SHOWNORMAL, ewNoWait, ErrorCode);
end;

procedure CreateDotNetPage;
var
  NetFrameWorkInstalled: Boolean;
  StaticText: TNewStaticText;
  StaticText2: TNewStaticText;
  Page: TWizardPage;
  URLLabel: TNewStaticText;
begin
  NetFrameWorkInstalled := RegKeyExists(HKLM,'SOFTWARE\Microsoft\.NETFramework\policy\v2.0');
  if NetFrameWorkInstalled = false then begin

    Page := CreateCustomPage(wpWelcome, 'Microsoft .NET Framework 2.0 not found', 'An optional component is missing');

    StaticText := TNewStaticText.Create(Page);
    StaticText.Caption := 'The installer has detected Microsoft .NET Framework 2.0 is not installed on this computer.' #13#13 'You can still use Wintermute Engine Development Kit, but some of the additional tools will not be available until you install .NET Framework.' #13;
    StaticText.Width := Page.SurfaceWidth;
    StaticText.AutoSize := True;
    StaticText.WordWrap := True;
    StaticText.Parent := Page.Surface;

    URLLabel := TNewStaticText.Create(Page);
    URLLabel.Caption := 'Visit Microsoft .NET Framework 2.0 download page';
    URLLabel.Cursor := crHand;
    URLLabel.OnClick := @DotNetURLClick;
    URLLabel.Parent := Page.Surface;
    URLLabel.Font.Style := URLLabel.Font.Style + [fsUnderline];
    URLLabel.Font.Color := clBlue;
    URLLabel.Top := StaticText.Top + StaticText.Height;

    StaticText2 := TNewStaticText.Create(Page);
    StaticText2.Caption := 'Microsoft .NET Framework is a modern application development platform for Windows operating systems. Some of the Wintermute Engine tools are taking advantage of this platform.';
    StaticText2.Width := Page.SurfaceWidth;
    StaticText2.AutoSize := True;
    StaticText2.WordWrap := True;
    StaticText2.Top := URLLabel.Top + URLLabel.Height + ScaleY(50);
    StaticText2.Parent := Page.Surface;
  end;
end;


procedure InitializeWizard();
begin
  CreateDotNetPage;
end;

